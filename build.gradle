plugins {
    id 'application'
    id 'idea'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.50'
    id 'com.google.protobuf' version '0.8.10'
    id "com.github.ben-manes.versions" version "0.27.0"
    id 'maven-publish'
}

group = 'io.prometheus'
version = '1.3.11-SNAPSHOT'
sourceCompatibility = '1.8'

def kotlinVersion = '1.3.50'
def serializationVersion = '0.13.0'
def coroutinesVersion = '1.3.2'
def utilsVersion = '1.0.5'

def grpcVersion = '1.25.0'
def protocVersion = '3.10.0'

def ktorVersion = '1.2.4'
def annotationVersion = '1.3.2'
def simpleVersion = '0.8.0'
def loggingVersion = '1.7.6'
def sparkVersion = '2.9.1'
def dropwizardVersion = '4.1.1'
def zipkinVersion = '5.9.0'
def zipkenSenderVersion = '2.11.0'
def kluentVersion = '1.56'
def tscfgVersion = '1.3.4'
def contribVersion = '0.8.1'
def jcommanderVersion = '1.78'
def logbackVersion = '1.2.3'
def slf4jVersion = '1.7.28'

repositories {
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url = 'https://dl.bintray.com/kotlin/kotlin-dev/' }
    maven { url = 'https://maven-central.storage-download.googleapis.com/repos/central/data/' }
    mavenCentral()
    jcenter()
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlinVersion}"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${serializationVersion}"
    
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:${coroutinesVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-debug:${coroutinesVersion}"

    implementation "io.grpc:grpc-all:${grpcVersion}"

    implementation "com.github.pambrose:common-utils:${utilsVersion}"

    implementation "javax.annotation:javax.annotation-api:${annotationVersion}"
    implementation "com.beust:jcommander:${jcommanderVersion}"
    implementation "com.typesafe:config:${tscfgVersion}"
    implementation "com.sparkjava:spark-core:${sparkVersion}"
    implementation "com.salesforce.servicelibs:grpc-contrib:${contribVersion}"

    implementation "io.prometheus:simpleclient:${simpleVersion}"
    implementation "io.prometheus:simpleclient_hotspot:${simpleVersion}"
    implementation "io.prometheus:simpleclient_servlet:${simpleVersion}"
    implementation "io.prometheus:simpleclient_jetty:${simpleVersion}"

    implementation "io.ktor:ktor-server-core:${ktorVersion}"
    implementation "io.ktor:ktor-server-cio:${ktorVersion}"
    implementation "io.ktor:ktor-client-core:${ktorVersion}"
    implementation "io.ktor:ktor-client-cio:${ktorVersion}"


    implementation "io.dropwizard.metrics:metrics-core:${dropwizardVersion}"
    implementation "io.dropwizard.metrics:metrics-healthchecks:${dropwizardVersion}"
    implementation "io.dropwizard.metrics:metrics-servlets:${dropwizardVersion}"
    implementation "io.dropwizard.metrics:metrics-jmx:${dropwizardVersion}"

    implementation "io.zipkin.brave:brave-instrumentation-sparkjava:${zipkinVersion}"
    implementation "io.zipkin.brave:brave-instrumentation-grpc:${zipkinVersion}"
    implementation "io.zipkin.reporter2:zipkin-sender-okhttp3:${zipkenSenderVersion }"

    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "org.slf4j:jul-to-slf4j:${slf4jVersion}"
    implementation "io.github.microutils:kotlin-logging:${loggingVersion}"

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${kotlinVersion}"
    testImplementation "org.amshove.kluent:kluent:${kluentVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/main/kotlin'
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

compileKotlin.dependsOn ':generateProto'

startScripts.enabled = false

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}


configurations.all {
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime',
                             '-Xuse-experimental=kotlinx.serialization.UnstableDefault',
                             '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes',
                             '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                             '-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi',
                             '-Xuse-experimental=io.ktor.util.KtorExperimentalAPI',
                             '-Xuse-experimental=kotlinx.serialization.UnstableDefault']
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-Xuse-experimental=kotlin.time.ExperimentalTime',
                             '-Xuse-experimental=kotlinx.serialization.UnstableDefault',
                             '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes',
                             '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                             '-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi',
                             '-Xuse-experimental=io.ktor.util.KtorExperimentalAPI',
                             '-Xuse-experimental=kotlinx.serialization.UnstableDefault']
    }
}


test {
    //useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
    }
}